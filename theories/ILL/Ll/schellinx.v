(**************************************************************)
(*   Copyright Dominique Larchey-Wendling [*]                 *)
(*                                                            *)
(*                             [*] Affiliation LORIA -- CNRS  *)
(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*         CeCILL v2 FREE SOFTWARE LICENSE AGREEMENT          *)
(**************************************************************)

Require Import List Permutation.

From Undecidability.Shared.Libs.DLW 
  Require Import utils.

From Undecidability.ILL Require Import ILL.

Set Implicit Arguments.

(* Small inversion lemma *)

Fact app_eq_single_inv X (l m : list X) x :
       l++m = x::nil 
    -> l = nil /\ m = x::nil 
    \/ l = x::nil /\ m = nil.
Proof.
  intros H.
  destruct l as [ | y l ]; auto.
  right.
  inversion H.
  destruct l; destruct m; auto; discriminate.
Qed.

Tactic Notation "app" "inv" "singleton" "in" hyp(H) :=
  apply app_eq_single_inv in H as [ (-> & ->) | (-> & ->) ].

Tactic Notation "app" "inv" "nil" "in" hyp(H) :=
  apply app_eq_nil in H as (-> & ->).

(** * Classical Linear Logic vs Intuitionnisitc Linear Logic 

       derived from the work of H. Schellinx JLC 91 *)

Local Infix "~p" := (@Permutation _) (at level 70).

Inductive cll_connective := cll_with | cll_plus | cll_limp | cll_times | cll_par.
Inductive cll_constant := cll_1 | cll_0 | cll_bot | cll_top.
Inductive cll_modality := cll_bang | cll_qmrk | cll_neg.

Inductive cll_form : Set :=
  | cll_var  : ll_vars -> cll_form
  | cll_cst  : cll_constant  -> cll_form
  | cll_una  : cll_modality -> cll_form -> cll_form
  | cll_bin  : cll_connective -> cll_form -> cll_form -> cll_form.

(* Symbols for cut&paste âŸ™   âŸ˜  ðŸ™ ï¹  âŠ—  âŠ•  âŠ¸  ?   â€¼  âˆ…  âŠ¢ âŸ¦ âŸ§ Î“ Î” Î£ *)

Section mapping_ill_to_cll.

  Reserved Notation "[ f ]" (at level 1).
  Reserved Notation "âŸ¨ f âŸ©" (at level 1).

  Fixpoint ill_cll f :=
    match f with
      | Â£ v  => cll_var v
      | âŸ˜ => cll_cst cll_bot
      | âŸ™ => cll_cst cll_top
      | ðŸ™ => cll_cst cll_1
      | !f => cll_una cll_bang [f]
      | fâŠ—g => cll_bin cll_times [f] [g]
      | f&g => cll_bin cll_with [f] [g]
      | fâŠ•g => cll_bin cll_plus [f] [g]
      | fâŠ¸g => cll_bin cll_limp [f] [g]
    end
  where "[ f ]" := (ill_cll f).

  Fixpoint cll_ill f :=
    match f with
      | cll_var v => Â£ v
      | cll_cst cll_bot => âŸ˜
      | cll_cst cll_top => âŸ™
      | cll_cst cll_1 => ðŸ™
      | cll_una cll_bang f => !âŸ¨fâŸ©
      | cll_bin cll_times f g => âŸ¨fâŸ© âŠ— âŸ¨gâŸ©
      | cll_bin cll_with  f g => âŸ¨fâŸ© & âŸ¨gâŸ©
      | cll_bin cll_plus  f g => âŸ¨fâŸ© âŠ• âŸ¨gâŸ©
      | cll_bin cll_limp  f g => âŸ¨fâŸ© âŠ¸ âŸ¨gâŸ©
      | _ => âŸ˜  (* arbitrary value *)
    end
  where "âŸ¨ f âŸ©" := (cll_ill f).

  Fixpoint from_ill f := 
    match f with
      | cll_var _ => True
      | cll_cst cll_bot => True
      | cll_cst cll_top => True
      | cll_cst cll_1 => True
      | cll_una cll_bang f => from_ill f
      | cll_bin cll_times f g => from_ill f /\ from_ill g
      | cll_bin cll_with  f g => from_ill f /\ from_ill g
      | cll_bin cll_plus  f g => from_ill f /\ from_ill g
      | cll_bin cll_limp  f g => from_ill f /\ from_ill g
      | _ => False
    end.

  Fact ill_cll_ill f : âŸ¨[f]âŸ©= f.
  Proof. induction f as [ | [] | | [] ]; simpl; f_equal; auto. Qed.

  Fact cll_ill_cll f : from_ill f -> [âŸ¨fâŸ©] = f.
  Proof.
    induction f as [ | [] | [] | [] ]; simpl; try tauto; intros; f_equal; tauto.
  Qed.

  Fact ill_cll_from_ill f : from_ill [f].
  Proof. induction f as [ | [] | | [] ]; simpl; tauto. Qed. 

  Fixpoint cll_has_bot f := 
    match f with
      | cll_var _ => False
      | cll_cst cll_bot => True
      | cll_una _ f => cll_has_bot f
      | cll_bin _ f g => cll_has_bot f \/ cll_has_bot g
      | _ => False
    end.

  Fixpoint ill_has_bot f := 
    match f with
      | ll_var _ => False
      | ll_zero ll_bot => True
      | ll_ban f => ill_has_bot f
      | ll_bin _ f g => ill_has_bot f \/ ill_has_bot g
      | _ => False
    end.

  Fact cll_ill_has_bot f : cll_has_bot f -> ill_has_bot âŸ¨fâŸ©.
  Proof. induction f as  [ | [] | [] | [] ]; simpl; tauto. Qed.

  Fact ill_cll_has_bot f : ill_has_bot f -> cll_has_bot [f].
  Proof. induction f as [ | [] | | [] ]; simpl; tauto. Qed.

  Fact ill_cll_has_bot_eq f : ill_has_bot f <-> cll_has_bot [f].
  Proof.
    split.
    + apply ill_cll_has_bot.
    + intros H.
      apply cll_ill_has_bot in H.
      now rewrite ill_cll_ill in H.
  Qed.

  Fixpoint from_ill_no_bot f := 
    match f with
      | cll_var _ => True
      | cll_cst cll_top => True
      | cll_cst cll_1 => True
      | cll_una cll_bang f => from_ill_no_bot f
      | cll_bin cll_times f g => from_ill_no_bot f /\ from_ill_no_bot g
      | cll_bin cll_with  f g => from_ill_no_bot f /\ from_ill_no_bot g
      | cll_bin cll_plus  f g => from_ill_no_bot f /\ from_ill_no_bot g
      | cll_bin cll_limp  f g => from_ill_no_bot f /\ from_ill_no_bot g
      | _ => False
    end.

  Fact from_ill_no_bot_from_ill f : from_ill_no_bot f -> from_ill f.
  Proof.
    induction f as [ | [] | [] | [] ]; simpl; tauto.
  Qed.

End mapping_ill_to_cll.

Section cut_free_cll.

  Notation "'Â£' x" := (cll_var x) (at level 1).

  Notation "âŸ™" := (cll_cst cll_top).
  Notation "âŸ˜" := (cll_cst cll_bot).
  Notation "ðŸ™" := (cll_cst cll_1).
  Notation "ðŸ˜" := (cll_cst cll_0).

  Infix "&" := (cll_bin cll_with) (at level 50).
  Infix "â…‹" := (cll_bin cll_par) (at level 50).
  Infix "âŠ—" := (cll_bin cll_times) (at level 50).
  Infix "âŠ•" := (cll_bin cll_plus) (at level 50).
  Infix "âŠ¸" := (cll_bin cll_limp) (at level 51, right associativity).

  Notation "'!' x" := (cll_una cll_bang x) (at level 52).
  Notation "'â€½' x" := (cll_una cll_qmrk x) (at level 52).

  Notation "â€¼ x" := (map (cll_una cll_bang) x) (at level 60).
  Notation "â‡ x" := (map (cll_una cll_qmrk) x) (at level 60).

  (* Symbols for cut&paste âŸ™   âŸ˜   ð  ï¹  âŠ—  âŠ•  âŠ¸  ! â€¼ â€½ â‡ âˆ…  âŠ¢ *)

  Notation "âˆ…" := nil.

  Reserved Notation "Î“ 'âŠ¢c' Î”" (at level 70, no associativity).

  (* All the rules of Cut-free CLL *)

  Inductive S_cll : list cll_form -> list cll_form -> Prop :=

    | in_cll_ax    : forall A,                         A::âˆ… âŠ¢c A::âˆ…

(*
    | in_cll_cut   : forall Î“ Î” Î“' Î”' A,       Î“ âŠ¢c A::Î”    ->   A::Î“' âŠ¢c Î”'
                                             (*-----------------------------*)    
                                        ->           Î“++Î“' âŠ¢c Î”++Î”'
*)

    | in_cll_perm  : forall Î“ Î” Î“' Î”',        Î“ ~p Î“'  ->  Î” ~p Î”'  ->  Î“ âŠ¢c Î” 
                                             (*-----------------------------*)
                                        ->              Î“' âŠ¢c Î”'

    | in_cll_limp_l : forall Î“ Î” Î“' Î”' A B,   Î“ âŠ¢c A::Î”      ->   B::Î“' âŠ¢c Î”'
                                             (*-----------------------------*)    
                                        ->         A âŠ¸ B::Î“++Î“' âŠ¢c Î”++Î”'

    | in_cll_limp_r : forall Î“ Î” A B,                 A::Î“ âŠ¢c B::Î”
                                             (*-----------------------------*)
                                        ->            Î“ âŠ¢c A âŠ¸ B::Î”

    | in_cll_with_l1 : forall Î“ Î” A B,                  A::Î“ âŠ¢c Î” 
                                             (*-----------------------------*)
                                        ->           A&B::Î“ âŠ¢c Î”

    | in_cll_with_l2 : forall Î“ Î” A B,                  B::Î“ âŠ¢c Î” 
                                             (*-----------------------------*)
                                        ->           A&B::Î“ âŠ¢c Î”
 
    | in_cll_with_r : forall Î“ Î” A B,          Î“ âŠ¢c A::Î”     ->   Î“ âŠ¢c B::Î”
                                             (*-----------------------------*)
                                        ->              Î“ âŠ¢c A&B::Î”

    | in_cll_times_l : forall Î“ A B Î”,               A::B::Î“ âŠ¢c Î” 
                                             (*-----------------------------*)
                                        ->            AâŠ—B::Î“ âŠ¢c Î”
 
    | in_cll_times_r : forall Î“ Î” Î“' Î”' A B,   Î“ âŠ¢c A::Î”    ->   Î“' âŠ¢c B::Î”'
                                             (*-----------------------------*)
                                        ->         Î“++Î“' âŠ¢c AâŠ—B::Î”++Î”'

    | in_cll_par_l : forall Î“ Î” Î“' Î”' A B,     A::Î“ âŠ¢c Î”    ->   B::Î“' âŠ¢c Î”'
                                             (*-----------------------------*)
                                        ->         Aâ…‹B::Î“++Î“' âŠ¢c Î”++Î”'

    | in_cll_par_r : forall Î“ A B Î”,                   Î“ âŠ¢c A::B::Î” 
                                             (*-----------------------------*)
                                        ->             Î“ âŠ¢c Aâ…‹B::Î”

    | in_cll_plus_l :  forall Î“ A B Î”,          A::Î“ âŠ¢c Î”  ->  B::Î“ âŠ¢c Î” 
                                             (*-----------------------------*)
                                        ->          AâŠ•B::Î“ âŠ¢c Î”

    | in_cll_plus_r1 : forall Î“ A B Î”,                  Î“ âŠ¢c A::Î”  
                                             (*-----------------------------*)
                                        ->              Î“ âŠ¢c AâŠ•B::Î”

    | in_cll_plus_r2 : forall Î“ A B Î”,                  Î“ âŠ¢c B::Î”  
                                             (*-----------------------------*)
                                        ->              Î“ âŠ¢c AâŠ•B::Î”

    | in_cll_bot_l : forall Î“ Î”,                     âŸ˜::Î“ âŠ¢c Î”

    | in_cll_top_r : forall Î“ Î”,                        Î“ âŠ¢c âŸ™::Î”

    | in_cll_unit_l : forall Î“ Î”,                       Î“ âŠ¢c Î”  
                                             (*-----------------------------*)
                                        ->           ðŸ™::Î“ âŠ¢c Î”

    | in_cll_unit_r :                                   âˆ… âŠ¢c ðŸ™::âˆ…

    | in_cll_zero_l :                        (*-----------------------------*)
                                             (* *)      ðŸ˜::âˆ… âŠ¢c âˆ…

    | in_cll_zero_r : forall Î“ Î”,                       Î“ âŠ¢c Î”  
                                             (*-----------------------------*)
                                        ->              Î“ âŠ¢c ðŸ˜::Î”


    | in_cll_bang_l : forall Î“ A Î”,                    A::Î“ âŠ¢c Î”
                                             (*-----------------------------*)
                                        ->            !A::Î“ âŠ¢c Î”

    | in_cll_bang_r : forall Î“ A Î”,                     â€¼Î“ âŠ¢c A::â‡Î”
                                             (*-----------------------------*)
                                        ->              â€¼Î“ âŠ¢c !A::â‡Î”

    | in_cll_qmrk_l : forall Î“ A Î”,                     A::â€¼Î“ âŠ¢c â‡Î”
                                             (*-----------------------------*)
                                        ->              â€½A::â€¼Î“ âŠ¢c â‡Î”

    | in_cll_qmrk_r : forall Î“ A Î”,                    Î“ âŠ¢c A::Î”
                                             (*-----------------------------*)
                                        ->             Î“ âŠ¢c â€½A::Î”

    | in_cll_weak_l : forall Î“ A Î”,                      Î“ âŠ¢c Î”
                                             (*-----------------------------*)
                                        ->           !A::Î“ âŠ¢c Î”

    | in_cll_weak_r : forall Î“ A Î”,                      Î“ âŠ¢c Î”
                                             (*-----------------------------*)
                                        ->               Î“ âŠ¢c â€½A::Î”

    | in_cll_cntr_l : forall Î“ A Î”,                !A::!A::Î“ âŠ¢c Î”
                                           (*-----------------------------*)
                                        ->             !A::Î“ âŠ¢c Î”

    | in_cll_cntr_r : forall Î“ A Î”,                    Î“ âŠ¢c â€½A::â€½A::Î”
                                           (*-----------------------------*)
                                        ->             Î“ âŠ¢c â€½A::Î”

  where "Î“ âŠ¢c Î”" := (S_cll Î“ Î”).

End cut_free_cll.

Notation "[ f ]" := (ill_cll f).
Notation "âŸ¨ f âŸ©" := (cll_ill f).

Notation "âŸ¦ Î“ âŸ§" := (map ill_cll Î“).
Notation "âŸª Î“ âŸ«" := (map cll_ill Î“).

Local Hint Resolve ill_cll_ill : core.

Fact ill_cll_ill_list Î“ : âŸªâŸ¦Î“âŸ§âŸ« = Î“.
Proof. induction Î“; simpl; f_equal; auto. Qed.

Fact ill_cll_lbang Î“ : âŸ¦â€¼Î“âŸ§ = map (cll_una cll_bang) âŸ¦Î“âŸ§.
Proof. induction Î“; simpl; f_equal; auto. Qed.

Fact cll_ill_lbang Î“ : âŸªmap (cll_una cll_bang) Î“âŸ« = â€¼âŸªÎ“âŸ«.
Proof. induction Î“; simpl; f_equal; auto. Qed.

Local Notation "Î“ 'âŠ¢i' A" := (S_ill Î“ A) (at level 70, no associativity).
Local Notation "Î“ 'âŠ¢c' Î”" := (S_cll Î“ Î”) (at level 70, no associativity).

Section ill_cll_is_sound.

  Hint Resolve Permutation_map : core.

  Theorem ill_cll_soundness Î“ A : Î“ âŠ¢i A -> âŸ¦Î“âŸ§ âŠ¢c [A]::âˆ….
  Proof.
    induction 1; simpl in *.
    + apply in_cll_ax.
    + apply (@in_cll_perm âŸ¦Î“âŸ§ ([A]::nil)); auto.
    + rewrite map_app. 
      now apply in_cll_limp_l with (Î” := âˆ…) (Î”' := _::_).
    + now apply in_cll_limp_r with (Î” := âˆ…).
    + now apply in_cll_with_l1.
    + now apply in_cll_with_l2.
    + now apply in_cll_with_r with (Î” := âˆ…).
    + now apply in_cll_bang_l.
    + rewrite ill_cll_lbang in *.
      now apply in_cll_bang_r with (Î” := âˆ…).
    + now apply in_cll_weak_l.
    + now apply in_cll_cntr_l.
    + now apply in_cll_times_l.
    + rewrite map_app.
      now apply in_cll_times_r with (Î” := âˆ…) (Î”' := âˆ…).
    + now apply in_cll_plus_l.
    + now apply in_cll_plus_r1.
    + now apply in_cll_plus_r2.
    + apply in_cll_bot_l.
    + apply in_cll_top_r.
    + now apply in_cll_unit_l.
    + apply in_cll_unit_r.
  Qed.

End ill_cll_is_sound.

Tactic Notation "solve" "Forall" :=
  repeat rewrite Forall_cons_inv in *;
  repeat rewrite Forall_app in *; simpl in *; tauto.

Section Schellinx_observation.

  (* One cannot get a cut free proof of Î“ âŠ¢c âˆ… unless âŸ˜ occurs in Î“ *)

  Notation "'Â£' x" := (cll_var x) (at level 1).

  Notation "âŸ™" := (cll_cst cll_top).
  Notation "âŸ˜" := (cll_cst cll_bot).
  Notation "ðŸ™" := (cll_cst cll_1).
  Notation "ðŸ˜" := (cll_cst cll_0).

  Infix "&" := (cll_bin cll_with) (at level 50).
  Infix "â…‹" := (cll_bin cll_par) (at level 50).
  Infix "âŠ—" := (cll_bin cll_times) (at level 50).
  Infix "âŠ•" := (cll_bin cll_plus) (at level 50).
  Infix "âŠ¸" := (cll_bin cll_limp) (at level 51, right associativity).

  Notation "'!' x" := (cll_una cll_bang x) (at level 52).
  Notation "'â€½' x" := (cll_una cll_qmrk x) (at level 52).

  Notation "â€¼ x" := (map (cll_una cll_bang) x) (at level 60).
  Notation "â‡ x" := (map (cll_una cll_qmrk) x) (at level 60).

  Notation "âˆ…" := nil.

  Let schellinx_rec Î“ Î” : Î“ âŠ¢c Î” -> Î” = âˆ… -> Forall from_ill Î“ -> exists f, In f Î“ /\ cll_has_bot f.
  Proof.
    induction 1 as [ A                                                        (* ax *)
                   | Î“ Î” Î“' Î”' H1 H2 H3 IH3                                   (* perm *)
                   | Î“ Î” Î“' Î”' A B H1 IH1 H2 IH2 | Î“ Î” A B H1 IH1             (* -o *)
                   | Î“ Î” A B H1 IH1 | Î“ Î” A B H1 IH1 | Î“ Î” A B H1 IH1 H2 IH2  (* & *)
                   | Î“ A B Î” H1 IH1 | Î“ Î” Î“' Î”' A B H1 IH1 H2 IH2             (* * *)
                   | Î“ Î” Î“' Î”' A B H1 IH1 H2 IH2 | Î“ A B Î” H1 IH1             (* par *)
                   | Î“ A B Î” H1 IH1 H2 IH2 | Î“ A B Î” H1 IH1 | Î“ A B Î” H1 IH1  (* + *)
                   | |                                                        (* bot, top *)
                   | Î“ Î” H1 IH1 |                                             (* unit *)
                   | |                                                        (* zero *) 
                   | Î“ A Î” H1 IH1 | Î“ A Î” H1 IH1                              (* bang *)
                   | Î“ A Î” H1 IH1 |                                           (* qmrk *)
                   | Î“ A Î” H1 IH1 |                                           (* weak *)
                   | Î“ A Î” H1 IH1 | ];                                        (* cntr *)
              try discriminate.
    + intros -> H'.
      apply Permutation_sym, Permutation_nil in H2 as ->; auto.
      destruct IH3 as (f & G1 & G2); auto.
      * revert H'; apply Permutation_Forall, Permutation_sym; auto.
      * exists f; split; auto.
        revert G1; now apply Permutation_in.
    + intros H H'.
      app inv nil in H.
      destruct IH2 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (A âŠ¸ B); simpl; split; auto.
      * exists f; split; auto.
        right; apply in_or_app; tauto.
    + intros -> H'.
      destruct IH1 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (A&B); simpl; auto.
      * exists f; simpl; auto.
    + intros -> H'.
      destruct IH1 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (A&B); simpl; auto.
      * exists f; simpl; auto.
    + intros -> H'.
      destruct IH1 as (f & [ <- | [ <- | ] ] & ?); auto.
      * solve Forall.
      * exists (AâŠ—B); simpl; auto.
      * exists (AâŠ—B); simpl; auto.
      * exists f; simpl; auto.
    + intros H H'.
      app inv nil in H.
      destruct IH1 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (Aâ…‹B); simpl; auto.
      * exists f; simpl; split; auto.
        rewrite in_app_iff; auto.
    + intros -> H'. 
      destruct IH1 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (AâŠ•B); simpl; auto.
      * exists f; simpl; auto.
    + exists âŸ˜; simpl; auto.
    + intros -> H'.
      destruct IH1 as (f & ? & ?); auto.
      * solve Forall. 
      * exists f; simpl; auto.
    + intros; solve Forall.
    + intros -> H'.
      destruct IH1 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (!A); simpl; auto.
      * exists f; simpl; auto.
    + destruct Î”; try discriminate; intros _ H'; simpl in *.
      destruct IH1 as (f & [ <- | ] & ?); auto.
      * solve Forall.
      * exists (â€½A); simpl; auto.
      * exists f; simpl; auto.
    + intros -> H'.
      destruct IH1 as (f & ? & ?); auto.
      * solve Forall.
      * exists f; simpl; auto.
    + intros -> H'.
      destruct IH1 as (f & [ <- | [ <- | ] ] & ?); auto.
      * solve Forall.
      * exists (!A); simpl; auto.
      * exists (!A); simpl; auto.
      * exists f; simpl; auto.
  Qed.

  Lemma Schellinx_observation Î“ : Î“ âŠ¢c âˆ… -> Forall from_ill Î“ -> exists f, In f Î“ /\ cll_has_bot f.
  Proof. intros; now apply schellinx_rec with (2 := eq_refl). Qed.

End Schellinx_observation.

Section cll_ill_soundness.

  Notation "'Â£' x" := (cll_var x) (at level 1).

  Notation "âŸ™" := (cll_cst cll_top).
  Notation "âŸ˜" := (cll_cst cll_bot).
  Notation "ðŸ™" := (cll_cst cll_1).
  Notation "ðŸ˜" := (cll_cst cll_0).

  Infix "&" := (cll_bin cll_with) (at level 50).
  Infix "â…‹" := (cll_bin cll_par) (at level 50).
  Infix "âŠ—" := (cll_bin cll_times) (at level 50).
  Infix "âŠ•" := (cll_bin cll_plus) (at level 50).
  Infix "âŠ¸" := (cll_bin cll_limp) (at level 51, right associativity).

  Notation "'!' x" := (cll_una cll_bang x) (at level 52).
  Notation "'â€½' x" := (cll_una cll_qmrk x) (at level 52).

  Notation "â€¼ x" := (map (cll_una cll_bang) x) (at level 60).
  Notation "â‡ x" := (map (cll_una cll_qmrk) x) (at level 60).

  Notation "âˆ…" := nil.

  Theorem cll_ill_rec Î“ Î” A : Î“ âŠ¢c Î” -> Î” = A::âˆ… -> Forall from_ill (A::Î“) -> âŸªÎ“âŸ« âŠ¢i âŸ¨AâŸ© \/ cll_has_bot A \/ exists f, In f Î“ /\ cll_has_bot f.
  Proof.
    intros H; revert H A.
    induction 1 as [ A                                                        (* ax *)
                   | Î“ Î” Î“' Î”' H1 H2 H3 IH3                                   (* perm *)
                   | Î“ Î” Î“' Î”' A B H1 IH1 H2 IH2 | Î“ Î” A B H1 IH1             (* -o *)
                   | Î“ Î” A B H1 IH1 | Î“ Î” A B H1 IH1 | Î“ Î” A B H1 IH1 H2 IH2  (* & *)
                   | Î“ A B Î” H1 IH1 | Î“ Î” Î“' Î”' A B H1 IH1 H2 IH2             (* * *)
                   | Î“ Î” Î“' Î”' A B H1 IH1 H2 IH2 | Î“ A B Î” H1 IH1             (* par *)
                   | Î“ A B Î” H1 IH1 H2 IH2 | Î“ A B Î” H1 IH1 | Î“ A B Î” H1 IH1  (* + *)
                   | |                                                        (* bot, top *)
                   | Î“ Î” H1 IH1 |                                             (* unit *)
                   | |                                                        (* zero *) 
                   | Î“ A Î” H1 IH1 | Î“ A Î” H1 IH1                              (* bang *)
                   | Î“ A Î” H1 IH1 |                                           (* qmrk *)
                   | Î“ A Î” H1 IH1 |                                           (* weak *)
                   | Î“ A Î” H1 IH1 | ];                                        (* cntr *)
               intros C HÎ” HÎ“.
    + inversion HÎ”; left; simpl; constructor.
    + subst.
      apply Permutation_sym, Permutation_length_1_inv in H2 as ->.
      destruct (IH3 _ eq_refl) as [ H | [ | (f & H & ?) ] ]; auto.
      * rewrite Forall_cons_inv in *.
        destruct HÎ“ as (? & HÎ“); split; auto.
        revert HÎ“; apply Permutation_Forall, Permutation_sym; auto.
      * left; apply in_ill3_perm with (2 := H).
        now apply Permutation_map.
      * do 2 right; exists f; split; auto.
        revert H; now apply Permutation_in.
    + app inv singleton in HÎ”.
      * destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; auto.
        - solve Forall.
        - destruct (IH2 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; auto.
          ++ solve Forall.
          ++ left; simpl; rewrite map_app; constructor; auto.
          ++ do 2 right; exists (AâŠ¸B); simpl; tauto.
          ++ do 2 right; exists f; split; auto; right; apply in_or_app; tauto.
        - do 2 right; exists (AâŠ¸B); simpl; tauto.
        - do 2 right; exists f; split; auto; right; apply in_or_app; tauto.
      * apply Schellinx_observation in H2 as (f & [ <- | ] & ?).
        - do 2 right; exists (AâŠ¸B); simpl; tauto.
        - do 2 right; exists f; split; auto; right; apply in_or_app; tauto.
        - solve Forall.
    + inversion HÎ”; subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; constructor; auto.
      * do 2 right; exists f; auto.
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; apply in_ill3_with_l1; auto.
      * do 2 right; exists (A&B); simpl; tauto.
      * do 2 right; exists f; auto.
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; apply in_ill3_with_l2; auto.
      * do 2 right; exists (A&B); simpl; tauto.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst; clear HÎ”.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * destruct (IH2 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
        - solve Forall.
        - left; simpl; constructor; auto.
        - do 2 right; exists f; simpl; tauto.
      * do 2 right; exists f; simpl; tauto. 
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | [ <- | ] ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; constructor; auto.
      * do 2 right; exists (AâŠ—B); simpl; tauto.
      * do 2 right; exists (AâŠ—B); simpl; tauto.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst.
      app inv nil in H3.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * destruct (IH2 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
        - solve Forall.
        - left; rewrite map_app; constructor; auto.
        - do 2 right; exists f; split; auto.
          apply in_or_app; auto.
      * do 2 right; exists f; split; auto.
        apply in_or_app; auto.
    + rewrite !Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
    + inversion HÎ”; subst.
      rewrite Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
    + subst. 
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * destruct (IH2 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; simpl; auto.
        - solve Forall. 
        - left; simpl; now constructor.
        - do 2 right; exists (AâŠ•B); simpl; tauto.
        - do 2 right; exists f; auto.
      * do 2 right; exists (AâŠ•B); simpl; tauto.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; now apply in_ill3_plus_r1.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; now apply in_ill3_plus_r2.
      * do 2 right; exists f; auto.
    + left; constructor.
    + inversion HÎ”; subst; left; constructor.
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; now constructor.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst; left; constructor.
    + discriminate.
    + inversion HÎ”; subst.
      rewrite Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; now constructor.
      * do 2 right; exists (!A); auto.
      * do 2 right; exists f; auto.
    + destruct Î”; try discriminate.
      inversion HÎ”; subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; rewrite cll_ill_lbang in *; now constructor.
      * do 2 right; exists f; auto.
    + destruct Î” as [ |  D [ ] ]; try discriminate.
      inversion HÎ”; subst.
      rewrite Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
    + inversion HÎ”; subst.
      rewrite Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & ? & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; now constructor.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst.
      rewrite Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
    + subst.
      destruct (IH1 _ eq_refl) as [ ? | [ | (f & [ <- | [ <- | ] ] & ?) ] ]; simpl; auto.
      * solve Forall.
      * left; now constructor.
      * do 2 right; exists (!A); simpl; auto.
      * do 2 right; exists (!A); simpl; auto.
      * do 2 right; exists f; auto.
    + inversion HÎ”; subst.
      rewrite Forall_cons_inv in HÎ“; simpl in HÎ“; tauto.
  Qed.

  (* If an ILL sequent is cut-free CLL provable then it is also cut-free ILL provable unless it contains âŸ˜ *)

  Theorem cll_ill_soundness Î“ A : âŸ¦Î“âŸ§ âŠ¢c [A]::âˆ… -> Î“ âŠ¢i A \/ ill_has_bot A \/ exists f, In f Î“ /\ ill_has_bot f.
  Proof.
    intros H.
    apply cll_ill_rec with (2 := eq_refl) in H.
    * rewrite ill_cll_ill_list, ill_cll_ill, <- ill_cll_has_bot_eq in H.
      destruct H as [ | [ | (f & G1 & G2) ] ]; auto.
      do 2 right.
      apply in_map_iff in G1.
      destruct G1 as (g & <- & ?).
      exists g; rewrite ill_cll_has_bot_eq; auto.
    * rewrite -> Forall_map with (f := ill_cll) (ll := A::Î“), Forall_forall.
      intros ? _; apply ill_cll_from_ill.
  Qed.

End cll_ill_soundness.

(* If the ILL sequent Î“ âŠ¢ A does not contain any occurences of âŸ˜   then 
   it is provable in ILL iff it is provable in CLL  *)

Theorem ill_cll_equiv Î“ A  : 
          (forall f, In f (A::Î“) -> ~ ill_has_bot f) 
       -> Î“ âŠ¢i A <-> âŸ¦Î“âŸ§ âŠ¢c [A]::âˆ….
Proof.
  intros H; split.
  + apply ill_cll_soundness.
  + intros H1. 
    apply cll_ill_soundness in H1 as [ | [ ? | (f & ? & ?) ] ]; auto.
    * destruct (H A); simpl; auto.
    * destruct (H f); simpl; auto.
Qed.

(**************************************************************)
(*   Copyright Dominique Larchey-Wendling [*]                 *)
(*                                                            *)
(*                             [*] Affiliation LORIA -- CNRS  *)
(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*         CeCILL v2 FREE SOFTWARE LICENSE AGREEMENT          *)
(**************************************************************)

Require Import List Arith Bool Lia Eqdep_dec.

From Undecidability.Problems
  Require Import Reduction PCP TM.

From Undecidability.Reductions
  Require Import TM_to_BPCP.

From Undecidability.Shared.Libs.DLW.Utils
  Require Import utils_tac utils_list utils_nat finite.

From Undecidability.Shared.Libs.DLW.Vec 
  Require Import pos vec.

From Undecidability.Shared.Libs.DLW.Wf 
  Require Import wf_finite.

From Undecidability.TRAKHTENBROT
  Require Import notations utils bpcp 
                 fo_sig fo_terms fo_logic fo_sat 

                 red_utils

                 Sig_discrete

                 BPCP_SigBPCP              (* from BPCP to a finitary signature *)
                 Sig_rem_syms              (* convert symbols into rels *)
                 Sig_uniform               (* convert to same arity for every rels *)
                 Sig_one_rel               (* many rels of arity n into one (n+1) and constants *)
                 Sig_rem_cst               (* replace constants with free variables *)
                 Sign_Sig2                 (* From R_n to R_2 *)
          (*       Sig3_Sig2                 (* From R_3 to R_2 *)  *)
                 Sig2_Sign                 (* Embed R_2 into R_n with n >= 2 *)
                 Sign_Sig                  (* Embed R_n into Î£ where R_n occurs in Î£ *)
                 Sig_Sig2                  (* Embed discrete Î£ into Î£2 = (Ã¸,{R_2}) *)
                 Sig_Sig_fin               (* Alternate path: Î£ -> Î£fin -> Î£2 *)
                 Sig2_Sig21                (* Embed Î£2 = (Ã¸,{R_2}) into Î£21 = ({f_2},{P_1}) *)
                 Sig2_SigSSn1              (* Embed Î£2 = (Ã¸,{R_2}) into Î£SSn1 = ({f_(2+n)},{P_1}) *)
                 Sign1_Sig                 (* Embed Î£n1 = ({f_n},{R_1}) into Î£ where
                                               f_n and R_1 occur into Î£ *)
                 .

Set Implicit Arguments.

(* Some ideas for notations and terminology

    Î£ = (âˆ… or { f_1 , g_1 , a_0, b_0 } ,{ âˆˆ_2 , =_2 , T_3, P_2 , â‰¡_2 , â‰º_3 })
    finite/listable <->   <âˆ ğ”½
    decidable/computable/Boolean <-> â„‚
    discrete/decidable equality <-> ğ”»
    interpreted equality <-> =

    We should fix that models have to be finite (listable) and
    computable/Boolean. May be the best terminology is Boolean.
    Discreteness is not a issue, see below. I tend tp favor finite
    over listable because the term is already used in classical
    logic, whereas "computable" means nothing for finite models
    (ie finiteness implies computability in a classical setting)

    Switching to weakly decidable models would be a problem
    also for recovering functions from relations ...

    To discuss also is the (small) issue of the empty model
    which makes sense only for closed formulas. In that case,
    the logic is reduced to True/False because âˆ€ <-> True
    and âˆƒ <-> False
*)

(* Summary of what is implement in here

    BPCP âª¯ SAT({f_1,g_1,a_0,b_0},{P_2,â‰¡_2,â‰º_2},ğ”½,â„‚)

    SAT(Î£,ğ”½,ğ”») âª¯  SAT(Î£,ğ”½,â„‚,ğ”»)  and   SAT(Î£,ğ”½,â„‚,ğ”») âª¯ SAT(Î£,ğ”½,ğ”»)

    SAT(sy,re,ğ”½,â„‚,ğ”») âª¯ SAT(âˆ…,sy+{=_2}+re,ğ”½,â„‚,=) (with sy finite or discrete)

    SAT(sy,re,ğ”½,â„‚,=) âª¯ SAT(sy,re,ğ”½,â„‚) (with =_2 of arity 2 in re)

    SAT(Î£,ğ”½,â„‚) âª¯ SAT(Î£unif Î£ n,ğ”½,â„‚)  (with all arities of rels in Î£ <= n)

    SAT(Ã¸,re_n,ğ”½,â„‚) âª¯ SAT(re_0,{R_{n+1}},ğ”½,â„‚)  (re is finite and uniform arity n)

    SAT(âˆ…,{T_3},ğ”½,â„‚,ğ”») âª¯ SAT(âˆ…,{âˆˆ_2},ğ”½,â„‚)

    SAT(âˆ…,{T_3,=_2},ğ”½,â„‚,=) âª¯ SAT(âˆ…,{âˆˆ_2},ğ”½,â„‚)

    SAT(âˆ…,{R_2},ğ”½,â„‚) âª¯ SAT(âˆ…,{R_n},ğ”½,â„‚)       (for 2 <= n)

    SAT(âˆ…,{R_n},ğ”½,â„‚) âª¯ SAT(Î£,ğ”½,â„‚)             (when Î£ contains a n-ary relation)

*)

(** With Î£ = (sy,re) a signature and =_2 : re and a proof that
    arity of =_2 is 2, there is a reduction from
    - finite and decidable and interpreted SAT over Î£ (=_2 is interpreted by =)
    - to finite and decidable SAT over Î£ 

        SAT(sy,re,ğ”½,â„‚,=) ---> SAT(sy,re,ğ”½,â„‚)  (with =_2 of arity 2 in re)
*)

(** The reduction from BPCP to SAT of a FO formula over a finitary & discrete signature
     - over signature Î£bpcp (2 unary funs, 2 constants, 3 rels)
     - within interpreted finite and decidable models

       BPCP --> SAT({f_1,g_1,a_0,b_0},{P_2,â‰¡_2,â‰º_2},ğ”½,â„‚)
  *)

Definition BPCP_input := list (list bool * list bool).
Definition BPCP_problem (lc : BPCP_input) := exists l, pcp_hand lc l l.

Theorem pcp_hand_derivable X lc u l : @pcp_hand X lc u l <-> derivable lc u l.
Proof. split; (induction 1; [ constructor 1 | constructor 2 ]; auto). Qed.

(** BPCP as defined in Problems/PCP.v is equivalent to BPCP_problem here *)

Theorem BPCP_BPCP_problem lc : BPCP_problem lc <-> BPCP lc.
Proof.
  unfold BPCP_problem; split.
  + intros (l & Hl).
    apply pcp_hand_derivable, derivable_BPCP in Hl.
    destruct Hl as (A & ? & ? & <- & ?); exists A; auto.
  + intros (A & ? & ? & ?).
    exists (tau2 A); apply pcp_hand_derivable, BPCP_derivable.
    exists A; auto.
Qed.

Theorem HaltTM_BPCP_problem : HaltTM 1 âª¯ BPCP_problem.
Proof.
  eapply reduces_transitive. exact HaltTM_BPCP.
  exists (fun x => x); symmetry; apply BPCP_BPCP_problem.
Qed.

Section BPCP_fo_fin_dec_SAT.

  Definition FIN_SAT_input := fol_form Î£bpcp.

  Theorem BPCP_FIN_DEC_EQ_SAT : BPCP_problem âª¯ @fo_form_fin_dec_eq_SAT Î£bpcp Î£bpcp_eq eq_refl.
  Proof.
    apply reduction_dependent; intros lc.
    exists (Î£bpcp_encode lc); split.
    + intros (l & Hl); revert Hl; apply Î£bpcp_encode_sound.
    + apply Î£bpcp_encode_complete.
  Qed.

End BPCP_fo_fin_dec_SAT.

Corollary BPCP_FSAT_Î£bpcp : BPCP_problem âª¯ FSAT Î£bpcp.
Proof.
  apply reduces_transitive with (1 := BPCP_FIN_DEC_EQ_SAT).
  apply FIN_DEC_EQ_SAT_FIN_DEC_SAT.
Qed.

Print Î£bpcp.
Check BPCP_FSAT_Î£bpcp.
(* Print Assumptions BPCP_FSAT_Î£bpcp. *)

(** With Î£  = (sy,re) a signature with finitely many term symbols (sy)
    and  Î£' = (Ã¸,sy+{=_2}+re) where =_2 is interpreted and the arity of symbols 
              in sy is augmented by 1
    then there is a reduction
    - from finite and discrete and decidable SAT over Î£
    - to finite and decidable and interpreted equality SAT over Î£'

        SAT(sy,re,ğ”½,â„‚,ğ”») ---> SAT(âˆ…,sy+{=_2}+re,ğ”½,â„‚,=)

    Another possible hypothesis is discreteness with sy

*)

Section FIN_DISCR_DEC_SAT_FIN_DEC_EQ_NOSYMS_SAT.

  Variable (Î£ : fo_signature) (HÎ£ : finite_t (syms Î£) + discrete (syms  Î£)).

  Theorem FIN_DISCR_DEC_SAT_FIN_DEC_EQ_NOSYMS_SAT :
          @fo_form_fin_discr_dec_SAT Î£
              âª¯ @fo_form_fin_dec_eq_SAT (Î£nosyms Î£) (inl tt) eq_refl.
  Proof.
    destruct HÎ£ as [ (l & Hl) | H ].
    - exists (fun A => Î£syms_Î£nosyms l A).
      intros A; split; intros (X & HX); exists X; revert HX.
      + apply Î£syms_Î£nosyms_sound.
      + apply Î£syms_Î£nosyms_complete.
        * left; auto.
        * intros ? ?; auto.
    - exists (fun A => Î£syms_Î£nosyms (fol_syms A) A).
      intros A; split; intros (X & HX); exists X; revert HX.
      + apply Î£syms_Î£nosyms_sound.
      + apply Î£syms_Î£nosyms_complete.
        * intros s; apply In_dec, H.
        * intro; auto.
  Qed.
      
End FIN_DISCR_DEC_SAT_FIN_DEC_EQ_NOSYMS_SAT.

Corollary FSAT_Î£nosyms Î£ : finite_t (syms Î£) -> FSAT Î£ âª¯ FSAT (Î£nosyms Î£).
Proof.
  intros H.
  apply reduces_transitive with (1 := FIN_DEC_SAT_FIN_DISCR_DEC_SAT _).
  apply reduces_transitive with (2 := @FIN_DEC_EQ_SAT_FIN_DEC_SAT (Î£nosyms Î£) (inl tt) eq_refl).
  apply FIN_DISCR_DEC_SAT_FIN_DEC_EQ_NOSYMS_SAT; auto.
Qed.

Print Î£nosyms.
Check FSAT_Î£nosyms.
(* Print Assumptions FSAT_Î£nosyms. *)

(** If the relation symbols in Î£ have all their 
    arities upper bounded by n and 
    Î£unif n is the signature with the same functions
    symbols and relations symbols as Î£ except 
    that the arity of relations is uniformly equal 
    to n, then there is a reduction

      SAT(Î£,ğ”½,â„‚) ---> SAT(Î£unif n,ğ”½,â„‚)  
*)

Theorem FSAT_UNIFORM Î£ n :
             (forall r : rels Î£, ar_rels _ r <= n)
          -> FSAT Î£ âª¯ FSAT (Î£unif Î£ n).
Proof.
  intros Hn.
  exists (fun A => @Î£uniformize Î£ n (fol_rels A) A); intros A. 
  split; intros (X & HX); exists X; revert HX.
  + apply Î£uniformize_sound; auto.
  + intros H; generalize H.
    intros (_ & _ & _ & phi & _).
    revert H; apply Î£uniformize_complete; cbv; auto.
Qed.

Print Î£unif.
Check FSAT_UNIFORM.
(* Print Assumptions FSAT_UNIFORM. *)

(** With Î£=(sy,re) a signature with an empty type of term symbols
    and where all the finitely many relations in re have the same 
    uniform arity n there is a reduction
    - from finite & decidable SAT over Î£ 
    - to finite & decodable SAT over Î£=(re,{R)}
    where re become constants symbols and R is a single 
    relation of arity n+1
 *)

Theorem FSAT_ONE_REL Î£ n :
             (syms Î£ -> False)
          -> (forall r : rels Î£, ar_rels _ r = n)
          -> finite_t (rels Î£)
          -> FSAT Î£ âª¯ FSAT (Î£one_rel Î£ n).
Proof.
  intros Hs Hn (lr & Hr).
  exists (Î£unif_one_rel Hs Hn); intros A; split.
  + intros (X & M & H1 & H2 & phi & H3).
    exists (X + rels Î£)%type, (Î£unif_one_rel_model Hn M (phi 0)).
    exists.
    { apply finite_t_sum; auto; exists lr; auto. }
    exists.
    { intros [] v; vec split v with x; destruct x; simpl; try tauto; apply H2. }
    exists (fun n => inl (phi n)).
    revert H3; apply Î£unif_one_rel_sound.
  + intros (X & M' & H1 & H2 & phi & H3).
    exists X, (Î£one_unif_rel_model Hs Hn M'), H1.
    exists.
    { intros ? ?; apply H2. }
    exists phi.
    revert H3; apply Î£unif_one_rel_complete.
Qed.

Print Î£one_rel.
Check FSAT_ONE_REL.
(* Print Assumptions FSAT_ONE_REL. *)

(** With Î£=(sy,re) a signature with a discrete type sy of term symbols
    and among them, only constant symbols, there is a reduction
    - from finite & decidable SAT over Î£ 
    - to finite & decodable SAT over (Ã¸,re)
 *)

Theorem FSAT_NOCST Î£ :
             (forall s, ar_syms Î£ s = 0)
          -> discrete (syms Î£)
          -> FSAT Î£ âª¯ FSAT (Î£rem_cst Î£).
Proof.
  intros; apply reduction_dependent.
  apply Sig_rem_cst_dep_red; auto.
Qed.

Print Î£rem_cst.

Check FSAT_NOCST.
(* Print Assumptions FSAT_NOCST. *)

(*

(** With Î£rel 3 signature with a unique ternary symbol
     and Î£rel 2 signature with a unique binary symbol
   the reduction from 
   - finite and decidable and discrete SAT over Î£rel 3
   - to finite and decidable SAT over Î£rel 2 

      SAT(âˆ…,{T_3},ğ”½,â„‚,ğ”») ---> SAT(âˆ…,{âˆˆ_2},ğ”½,â„‚)
*)

Theorem FIN_DISCR_DEC_3SAT_FIN_DEC_2SAT : @fo_form_fin_discr_dec_SAT (Î£rel 3)
                                                                        âª¯ @fo_form_fin_dec_SAT (Î£rel 2).
Proof.
  exists Î£3_Î£2_enc; intros A; split.
  + apply SAT3_SAT2.
  + intros H; apply fo_form_fin_dec_SAT_fin_discr_dec, SAT2_SAT3, H.
Qed.

Corollary FSAT_REL_3to2 : FSAT (Î£rel 3) âª¯ FSAT (Î£rel 2).
Proof.
  apply reduces_transitive with (1 := FIN_DEC_SAT_FIN_DISCR_DEC_SAT _).
  apply FIN_DISCR_DEC_3SAT_FIN_DEC_2SAT.
Qed.

Check FSAT_REL_3to2.
Print Assumptions FSAT_REL_3to2.

*)

(** With Î£rel n signature with a unique n-ary symbol
     and Î£rel 2 signature with a unique binary symbol
   the reduction from 
   - finite and decidable and discrete SAT over Î£rel n
   - to finite and decidable SAT over Î£rel 2 

      SAT(âˆ…,{R_n},ğ”½,â„‚,ğ”») ---> SAT(âˆ…,{âˆˆ_2},ğ”½,â„‚)
*)

Theorem FIN_DISCR_DEC_nSAT_FIN_DEC_2SAT n : 
       @fo_form_fin_discr_dec_SAT (Î£rel n) âª¯ @fo_form_fin_dec_SAT (Î£rel 2).
Proof.
  exists (@Î£n_Î£2_enc n); intros A; split.
  + apply SATn_SAT2.
  + intros H; apply fo_form_fin_dec_SAT_fin_discr_dec, SAT2_SATn, H.
Qed.

Corollary FSAT_REL_nto2 n : FSAT (Î£rel n) âª¯ FSAT (Î£rel 2).
Proof.
  apply reduces_transitive with (1 := FIN_DEC_SAT_FIN_DISCR_DEC_SAT _).
  apply FIN_DISCR_DEC_nSAT_FIN_DEC_2SAT.
Qed.

Check FSAT_REL_nto2.
(* Print Assumptions FSAT_REL_nto2. *)

Theorem FSAT_REL2_to_FUN2REL1 : FSAT (Î£rel 2) âª¯ FSAT Î£21.
Proof.
  exists Î£2_Î£21_enc; intros A; split.
  + intros (X & M2 & H1 & H2 & phi & H3).
    apply Î£2_Î£21_enc_sound with (1 := H3); auto.
  + intros (Y & M21 & H1 & H2 & psi & H3).
    apply Î£2_Î£21_enc_complete with (2 := H3); auto.
Qed.

Print Î£21.

Check FSAT_REL2_to_FUN2REL1.
(* Print Assumptions FSAT_REL2_to_FUN2REL1. *)

Theorem FSAT_REL2_to_FUNnREL1 n : 2 <= n -> FSAT (Î£rel 2) âª¯ FSAT (Î£n1 n).
Proof.
  intros Hn; destruct n as [ | [ | n ] ]; try (exfalso; lia); clear Hn.
  exists (Î£2_Î£SSn1_enc n); intros A; split.
  + intros (X & M2 & H1 & H2 & phi & H3).
    apply Î£2_Î£SSn1_enc_sound with (1 := H3); auto.
  + intros (Y & M21 & H1 & H2 & psi & H3).
    apply Î£2_Î£SSn1_enc_complete with (2 := H3); auto.
Qed.

Print Î£n1.

Check FSAT_REL2_to_FUNnREL1.
(* Print Assumptions FSAT_REL2_to_FUNnREL1. *)

Theorem FSAT_FUNnREL1_ANY Î£ n f r : 
   ar_syms Î£ f = n -> ar_rels Î£ r = 1 -> FSAT (Î£n1 n) âª¯ FSAT Î£.
Proof.
  intros H1 H2.
  apply reduction_dependent.
  intros A.
  exists (Î£n1_Î£ _ _ _ H1 H2 A).
  apply Î£n1_Î£_correct.
Qed.

Check FSAT_FUNnREL1_ANY.
(* Print Assumptions FSAT_FUNnREL1_ANY. *)

(*      SAT(âˆ…,{R_2},ğ”½,â„‚) ---> SAT(âˆ…,{R_(2+n)},ğ”½,â„‚)           *)

Theorem FSAT_REL_2ton n :
                 2 <= n 
              -> FSAT (Î£rel 2)
                           âª¯ FSAT (Î£rel n).
Proof.
  revert n; intros [ | [ | n ] ] H; try (exfalso; lia).
  exists (Î£2_Î£n n); intros A; split.
  + apply Î£2_Î£n_soundness.
  + apply Î£2_Î£n_completeness.
Qed.

Check FSAT_REL_2ton.
(* Print Assumptions FSAT_REL_2ton. *)

(** If Î£ contains an n-ary relational symbol then there is a 
    reduction 

               SAT(âˆ…,{R_n},ğ”½,â„‚) ---> SAT(Î£,ğ”½,â„‚)  *)

Theorem FSAT_RELn_ANY Î£ n r : ar_rels Î£ r = n -> FSAT (Î£rel n) âª¯ FSAT Î£.
Proof.
  intros Hr.
  destruct (SATn_SAT_reduction _ _ Hr) as (f & Hf).
  exists f; apply Hf.
Qed.

Check FSAT_RELn_ANY.
(* Print Assumptions FSAT_RELn_ANY. *)

Section FINITARY_TO_BINARY.

  Variable (Î£ : fo_signature)
           (HÎ£1 : finite_t (syms Î£)) (HÎ£2 : discrete (syms Î£))
           (HÎ£3 : finite_t (rels Î£)) (HÎ£4 : discrete (rels Î£)).

  Let max_syms : { n | forall s, ar_syms Î£ s <= n }.
  Proof. 
    destruct HÎ£1 as (l & Hl).
    exists (lmax (map (ar_syms _) l)).
    intros s; apply lmax_prop, in_map_iff.
    exists s; auto.
  Qed.

  Let max_rels : { n | forall s, ar_rels Î£ s <= n }.
  Proof. 
    destruct HÎ£3 as (l & Hl).
    exists (lmax (map (ar_rels _) l)).
    intros r; apply lmax_prop, in_map_iff.
    exists r; auto.
  Qed.

  Hint Resolve finite_t_sum finite_sum finite_t_finite finite_t_unit.

  Theorem FINITARY_TO_BINARY : FSAT Î£ âª¯ FSAT (Î£rel 2).
  Proof.
    destruct max_syms as (ns & Hns).
    destruct max_rels as (nr & Hnr).
    set (m := lmax (2::(S ns)::nr::nil)).
    eapply reduces_transitive. 
    { apply FSAT_Î£nosyms; auto. }
    eapply reduces_transitive.
    { apply FSAT_UNIFORM with (n := m).
      intros [ [] | [] ].
      + apply lmax_prop; simpl; auto.
      + apply le_trans with (S ns).
        * simpl; apply le_n_S, Hns.
        * apply lmax_prop; simpl; auto.
      + apply le_trans with nr.
        * simpl; auto.
        * apply lmax_prop; simpl; auto. }
    eapply reduces_transitive.
    { apply FSAT_ONE_REL; simpl; auto; intros []. }
    eapply reduces_transitive.
    { apply FSAT_NOCST; simpl; auto. }
    apply (FSAT_REL_nto2 (S m)).
  Qed.

End FINITARY_TO_BINARY.

Check FINITARY_TO_BINARY.
(* Print Assumptions FINITARY_TO_BINARY. *)

Section DISCRETE_TO_BINARY.

  Variable (Î£ : fo_signature)
           (HÎ£1 : discrete (syms Î£))
           (HÎ£2 : discrete (rels Î£)).

  Theorem DISCRETE_TO_BINARY : FSAT Î£ âª¯ FSAT (Î£rel 2).
  Proof.
    apply reduction_dependent.
    intros A; exists (Î£_Î£2_enc HÎ£1 HÎ£2 A); split.
    + intros H; apply SAT_SAT2, fo_form_fin_dec_SAT_discr_equiv; auto.
    + apply SAT2_SAT.
  Qed.

End DISCRETE_TO_BINARY.

Check DISCRETE_TO_BINARY.
(* Print Assumptions DISCRETE_TO_BINARY. *)

Section DISCRETE_TO_BINARY_ALT.

  Variable (Î£ : fo_signature)
           (HÎ£1 : discrete (syms Î£))
           (HÎ£2 : discrete (rels Î£)).

  Theorem DISCRETE_TO_BINARY_ALT : FSAT Î£ âª¯ FSAT (Î£rel 2).
  Proof.
    apply reduction_dependent.
    intros A.
    destruct (Î£_finite HÎ£1 HÎ£2 A) as (Î£' & H1 & H2 & H3 & H4 & _ & _ & _ & _ & _ & _ & _ & _ & B & HB).
    destruct (FINITARY_TO_BINARY H1 H3 H2 H4) as (f & Hf).
    exists (f B). 
    rewrite <- Hf; apply HB.
  Qed.

End DISCRETE_TO_BINARY_ALT.

Check DISCRETE_TO_BINARY_ALT.
(* Print Assumptions DISCRETE_TO_BINARY_ALT. *)

Section FULL_TRAKHTENBROT.

  Let finite_t_bpcp_syms : finite_t Î£bpcp_syms.
  Proof. 
    exists (Î£bpcp_bool true::Î£bpcp_bool false::Î£bpcp_unit::Î£bpcp_undef::nil).
    intros [ [] | | ]; simpl; auto.
  Qed.

  Let discrete_bpcp_syms : discrete Î£bpcp_syms.
  Proof. red; repeat decide equality. Qed.

  Let finite_t_bpcp_rels : finite_t Î£bpcp_rels.
  Proof. 
    exists (Î£bpcp_hand::Î£bpcp_ssfx::Î£bpcp_eq::nil).
    intros []; simpl; auto.
  Qed.

  Let discrete_bpcp_rels : discrete Î£bpcp_rels.
  Proof. red; repeat decide equality. Qed.

  Let BPCP_Sig2 : BPCP_problem âª¯ FSAT (Î£rel 2).
  Proof.
    apply reduces_transitive with (1 := BPCP_FSAT_Î£bpcp).
    apply FINITARY_TO_BINARY; auto.
  Qed.

  Theorem FULL_TRAKHTENBROT Î£ r : 2 <= ar_rels Î£ r -> BPCP_problem âª¯ FSAT Î£.
  Proof.
    intros Hr.
    apply reduces_transitive with (1 := BPCP_Sig2).
    apply reduces_transitive with (1 := FSAT_REL_2ton Hr).
    apply FSAT_RELn_ANY with (1 := eq_refl).
  Qed.

  Theorem ALHTENBROT Î£ f r : 2 <= ar_syms Î£ f -> ar_rels Î£ r = 1 -> BPCP_problem âª¯ FSAT Î£.
  Proof.
    intros H1 H2.
    apply reduces_transitive with (1 := BPCP_Sig2).
    apply reduces_transitive with (1 := FSAT_REL2_to_FUNnREL1 H1).
    apply FSAT_FUNnREL1_ANY with f r; auto.
  Qed.
 
End FULL_TRAKHTENBROT.

(**************************************************************)
(*   Copyright Dominique Larchey-Wendling [*]                 *)
(*                                                            *)
(*                             [*] Affiliation LORIA -- CNRS  *)
(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*         CeCILL v2 FREE SOFTWARE LICENSE AGREEMENT          *)
(**************************************************************)

(** 1) Object-level representation of Diophantine logic 
    2) Closure properties for Diophantine relations and functions 
    3) Tactics for the automagic recognition of Diophantine shapes
*)

Require Import Arith Nat Omega.

From Undecidability.Shared.Libs.DLW.Utils 
  Require Import gcd.

Set Implicit Arguments.

(** Standard De Bruijn extension and De Bruijn projection *)

(* Fixpoint instead of Definition because of better unfolding properties *)

Fixpoint de_bruijn_ext {X} (ŒΩ : nat -> X) x n { struct n } :=
  match n with
    | 0   => x
    | S n => ŒΩ n
  end.

Notation "x ¬∑ ŒΩ" := (de_bruijn_ext ŒΩ x) (at level 1, format "x ¬∑ ŒΩ", left associativity).
Notation "ŒΩ ‚≠≥" := (fun n => ŒΩ (S n)) (at level 1, format "ŒΩ ‚≠≥", no associativity).

Fact de_bruijn_ext_proj X (ŒΩ : nat -> X) x n : (x¬∑ŒΩ)‚≠≥ n = ŒΩ n.
Proof. reflexivity. Qed.

Fact de_bruijn_proj_ext X (ŒΩ : nat -> X) n : (ŒΩ 0)¬∑(ŒΩ‚≠≥) n = ŒΩ n.
Proof. destruct n; reflexivity. Qed.

(** We factor out +/* and ‚à®/‚àß *)

Inductive dio_op := do_add | do_mul.

(* do_add represents both + and ‚à®
   do_mul represents both * and ‚àß *)  

Definition de_op_sem (o : dio_op) :=
  match o with
    | do_add => plus
    | do_mul => mult
  end.

Definition df_op_sem (o : dio_op) :=
  match o with
    | do_add => or
    | do_mul => and
  end.

(* De Bruijn syntax for diophantine formulas of the form

       A,B ::= x ‚âê ‚åûn‚åü | x ‚âê y | x ‚âê y ‚®¢ z | x ‚âê y ‚®∞ z | A ‚àß B | A ‚à® B | ‚àÉA 

          where x,y,z are variables 
           and  n is a natural number constant 

  In the De Bruijn syntax, variables and parameters are not
  distinguished and free (ie not captured) variables can serve
  as parameters

*)

Inductive dio_formula : Set :=
  | df_cst  : forall (x : nat) (n : nat), dio_formula
  | df_eq   : forall (x y : nat), dio_formula
  | df_op   : forall (o : dio_op) (x y z : nat), dio_formula 
  | df_bin  : forall (o : dio_op) (f g : dio_formula), dio_formula
  | df_exst : dio_formula -> dio_formula.

Notation df_add := (df_op do_add).
Notation df_mul := (df_op do_mul).
Notation df_conj := (df_bin do_mul).
Notation df_disj := (df_bin do_add).

Notation "‚àÉ x" := (df_exst x) (at level 54, right associativity).
Notation "x ‚àß y" := (df_conj x y) (at level 51, right associativity, format "x  ‚àß  y").
Notation "x ‚à® y" := (df_disj x y) (at level 52, right associativity, format "x  ‚à®  y").

Local Notation "x ‚âê ‚åû n ‚åü" := (df_cst x n) 
      (at level 49, no associativity, format "x  ‚âê  ‚åû n ‚åü").
Local Notation "x ‚âê y" := (df_eq x y) 
      (at level 49, no associativity, format "x  ‚âê  y").
Local Notation "x ‚âê y ‚®¢ z" := (df_add x y z) 
      (at level 49, no associativity, y at next level, format "x  ‚âê  y  ‚®¢  z").
Local Notation "x ‚âê y ‚®∞ z" := (df_mul x y z) 
      (at level 49, no associativity, y at next level, format "x  ‚âê  y  ‚®∞  z").

Local Reserved Notation "'‚ü¶' t '‚üß'" (at level 1, format "‚ü¶ t ‚üß").
Local Reserved Notation "f '‚¶É' œÉ '‚¶Ñ'" (at level 1, format "f ‚¶É œÉ ‚¶Ñ").

Section diophantine_logic.

  Fixpoint df_size f :=
    match f with
      | df_cst _ _    => 3
      | df_eq _ _     => 3
      | df_op _ _ _ _ => 5
      | df_bin _ f g  => 1 + df_size f + df_size g   
      | df_exst f     => 1 + df_size f
    end.

  (* size in Z (binary integers) can be computed for big formulas
     but size in nat usually fails *)

  Fixpoint df_size_Z f :=
    (match f with
      | df_cst _ _    => 3
      | df_eq _ _     => 3
      | df_op _ _ _ _ => 5
      | df_bin _ f g  => 1 + df_size_Z f + df_size_Z g   
      | df_exst f     => 1 + df_size_Z f
    end)%Z.

  Fact df_size_Z_spec f : df_size_Z f = Z.of_nat (df_size f).
  Proof.
    induction f as [ | | | ? f Hf g Hg | f Hf ]; simpl df_size;
      rewrite Nat2Z.inj_succ; try rewrite Nat2Z.inj_add; unfold df_size_Z; fold df_size_Z; auto; try omega.
  Qed.

  (** The semantics of Diophantine logic *)

  Fixpoint df_pred f ŒΩ :=
    match f with
      | df_cst x n     => ŒΩ x = n
      | df_eq x y      => ŒΩ x = ŒΩ y
      | df_op  o x y z => ŒΩ x = de_op_sem o (ŒΩ y) (ŒΩ z)
      | df_bin o f g   => df_op_sem o (‚ü¶f‚üß ŒΩ) (‚ü¶g‚üß ŒΩ)
      | df_exst f      => exists n, ‚ü¶f‚üß n¬∑ŒΩ
    end
  where "‚ü¶ f ‚üß" := (df_pred f).

  (** Fixpoint equations if needed and for readability *)

  Fact df_pred_cst x n ŒΩ : ‚ü¶x ‚âê ‚åûn‚åü‚üß ŒΩ = (ŒΩ x = n).
  Proof. reflexivity. Qed.

  Fact df_pred_eq x y ŒΩ : ‚ü¶x ‚âê y‚üß ŒΩ = (ŒΩ x = ŒΩ y).
  Proof. reflexivity. Qed.

  Fact df_pred_add x y z ŒΩ : ‚ü¶x ‚âê y ‚®¢ z‚üß ŒΩ = (ŒΩ x = ŒΩ y + ŒΩ z).
  Proof. reflexivity. Qed.

  Fact df_pred_mul x y z ŒΩ : ‚ü¶x ‚âê y ‚®∞ z‚üß ŒΩ = (ŒΩ x = ŒΩ y * ŒΩ z).
  Proof. reflexivity. Qed.
  
  Fact df_pred_conj f g ŒΩ : ‚ü¶f ‚àß g‚üß ŒΩ = (‚ü¶f‚üß ŒΩ /\ ‚ü¶g‚üß ŒΩ).
  Proof. reflexivity. Qed.

  Fact df_pred_disj f g ŒΩ : ‚ü¶f ‚à® g‚üß ŒΩ = (‚ü¶f‚üß ŒΩ \/ ‚ü¶g‚üß ŒΩ).
  Proof. reflexivity. Qed.

  Fact df_pred_exst f ŒΩ : ‚ü¶‚àÉf‚üß ŒΩ = exists n, ‚ü¶f‚üß n¬∑ŒΩ.
  Proof. reflexivity. Qed.

  (** Extensionality *)

  Fact df_pred_ext f ŒΩ œâ : (forall x, ŒΩ x = œâ x) -> ‚ü¶f‚üß ŒΩ <-> ‚ü¶f‚üß œâ.
  Proof.
    revert ŒΩ œâ; induction f as [ | | [] | [] f Hf g Hg | f Hf ]; intros ŒΩ œâ H; simpl.
    1-4: rewrite !H; tauto.
    1-2: rewrite Hf, Hg; auto; tauto.
    split; intros (n & Hn); exists n; revert Hn; apply Hf;
        intros []; simpl; auto.
  Qed.

  (* Diophantine formula renaming under binders *)

  Definition der_lift œÅ x := match x with 0 => 0 | S x => S (œÅ x) end.

  Fixpoint df_ren œÅ f :=
    match f with
      | df_cst x n    => df_cst (œÅ x) n
      | df_eq x y     => df_eq (œÅ x) (œÅ y)
      | df_op o x y z => df_op o (œÅ x) (œÅ y) (œÅ z)
      | df_bin o f g  => df_bin o f‚¶ÉœÅ‚¶Ñ g‚¶ÉœÅ‚¶Ñ
      | df_exst f     => df_exst f‚¶Éder_lift œÅ‚¶Ñ
    end
  where "f ‚¶É œÅ ‚¶Ñ" := (df_ren œÅ f).

  Fact df_ren_size œÅ f : df_size f‚¶ÉœÅ‚¶Ñ = df_size f.
  Proof. revert œÅ; induction f; intros; simpl; auto; do 2 f_equal; auto. Qed.

  Fact df_ren_size_Z œÅ f : df_size_Z f‚¶ÉœÅ‚¶Ñ = df_size_Z f.
  Proof. do 2 rewrite df_size_Z_spec; f_equal; apply df_ren_size. Qed.

  (** Renaming is compatible with semantics *)

  Fact df_pred_ren f ŒΩ œÅ : ‚ü¶f‚¶ÉœÅ‚¶Ñ‚üß ŒΩ <-> ‚ü¶f‚üß (fun x => ŒΩ (œÅ x)).
  Proof.
    revert ŒΩ œÅ; induction f as [ | | [] | [] f Hf g Hg | f Hf ]; intros ŒΩ œÅ; simpl; try tauto.
    1-2: rewrite Hf, Hg; tauto.
    split; intros (n & Hn); exists n; revert Hn; rewrite Hf;
        apply df_pred_ext; intros []; simpl; auto.
  Qed.

  Fact df_pred_lift f ŒΩ : ‚ü¶f‚¶ÉS‚¶Ñ‚üß ŒΩ <-> ‚ü¶f‚üß ŒΩ‚≠≥.
  Proof. apply df_pred_ren. Qed. 

End diophantine_logic.

Local Notation "‚ü¶ f ‚üß" := (df_pred f).
Local Notation "f ‚¶É œÅ ‚¶Ñ" := (df_ren œÅ f).

(** The notion of Diophantine relation, ie a relation extensionally 
    equivalent to the semantics of a Diophantine formula *)

Definition dio_rel R := { f | forall ŒΩ, ‚ü¶f‚üß ŒΩ <-> R ŒΩ }.
Notation ùîªR := dio_rel.

Section dio_rel_closure_properties.

  (** We establish closure properties of dio_rel / ùîªR
      These are proved by explicitely giving the witnesses 
      and we will systematically avoid that later on
      because in general, this is painful. 

      We use of the "abstract" tactic to hide Prop-only 
      content for better computations with those rich terms.

      Notice that those proofs are all closed with
      "Defined" and not "Qed" to allow computing with
      the informative part of terms, the one that is not
      hidden behind "abstract".

      The closure properties and the tactics developed
      below implement the automagic recognition of 

             "Diophantine shapes"
    *)
  
  Implicit Types R S : (nat -> nat) -> Prop.

  Fact dio_rel_cst x n : ùîªR (fun ŒΩ => ŒΩ x = n).
  Proof. exists (x ‚âê ‚åûn‚åü); abstract (intro; simpl; tauto). Defined.

  Fact dio_rel_eq x y : ùîªR (fun ŒΩ => ŒΩ x = ŒΩ y).
  Proof. exists (x ‚âê y); simpl; tauto. Defined.

  Fact dio_rel_add x y z : ùîªR (fun ŒΩ => ŒΩ x = ŒΩ y + ŒΩ z).
  Proof. exists (x ‚âê y ‚®¢ z); abstract (intro; simpl; tauto). Defined.

  Fact dio_rel_mul x y z : ùîªR (fun ŒΩ => ŒΩ x = ŒΩ y * ŒΩ z).
  Proof. exists (x ‚âê y ‚®∞  z); abstract (intro; simpl; tauto). Defined.
 
  Fact dio_rel_conj R S : ùîªR R -> ùîªR S -> ùîªR (fun ŒΩ => R ŒΩ /\ S ŒΩ).
  Proof.
    intros (fR & H1) (fS & H2); exists (fR ‚àß fS).
    abstract (intro; rewrite df_pred_conj, H1, H2; tauto).
  Defined.

  Fact dio_rel_disj R S : ùîªR R -> ùîªR S -> ùîªR (fun ŒΩ => R ŒΩ \/ S ŒΩ).
  Proof.
    intros (fR & H1) (fS & H2); exists (fR ‚à® fS).
    abstract (intro; rewrite df_pred_disj, H1, H2; tauto).
  Defined.

  Fact dio_rel_exst (K : nat -> (nat -> nat) -> Prop) : 
                 ùîªR (fun ŒΩ => K (ŒΩ 0) ŒΩ‚≠≥) -> ùîªR (fun ŒΩ => exists x, K x ŒΩ).
  Proof.
    intros (f & Hf); exists (‚àÉf). 
    abstract (intros ŒΩ; rewrite df_pred_exst;
      split; intros (n & Hn); exists n; revert Hn; rewrite Hf; simpl; auto).
  Defined.

  Fact dio_rel_equiv R S : (forall ŒΩ, S ŒΩ <-> R ŒΩ) -> ùîªR R -> ùîªR S.
  Proof. 
    intros H (f & Hf); exists f.
    abstract (intro; rewrite Hf, H; tauto).
  Defined.

  Fact dio_rel_ren R œÅ : ùîªR R -> ùîªR (fun ŒΩ => R (fun n => ŒΩ (œÅ n))).
  Proof.
    intros (f & Hf); exists (f‚¶ÉœÅ‚¶Ñ).
    abstract (intros; rewrite df_pred_ren, Hf; tauto).
  Defined.

End dio_rel_closure_properties.

(** From now on, we will quite systematically avoid directly
    manipulating the De Bruijn syntax *)

(** It is often more convenient to work with Diophantine functions 
    instead of Diophantine relations, eg 

        f : nat -> nat is a Diophantine function 
     if (fun x y => y = f x) is a Diophantine relation.
  
    We provide a tool for that here with associated tactics below 

*)

Definition dio_fun t := ùîªR (fun ŒΩ => ŒΩ 0 = t ŒΩ‚≠≥).
Notation ùîªF := dio_fun.

Fact dio_rel_fun_eq r t : ùîªF r -> ùîªF t -> ùîªR (fun ŒΩ => r ŒΩ = t ŒΩ).
Proof.
  intros H1 H2; red in H1, H2.
  apply dio_rel_equiv with (R := fun ŒΩ => exists x, x = r ŒΩ /\ x = t ŒΩ).
  + abstract (intros v; split;
     [ intros ->; exists (t v); auto 
     | intros (? & -> & ?); auto ]).
  + apply dio_rel_exst, dio_rel_conj; auto.
Defined.

Section utilities_for_better_efficiency.

  Ltac dio_sym H := now (apply dio_rel_equiv with (2 := H)). 

  Fact dio_rel_cst_sym x n : ùîªR (fun ŒΩ => n = ŒΩ x).
  Proof. dio_sym (dio_rel_cst x n). Defined.

  Fact dio_rel_add_sym x y z : ùîªR (fun ŒΩ => ŒΩ y + ŒΩ z = ŒΩ x).
  Proof. dio_sym (dio_rel_add x y z). Defined.

  Fact dio_rel_mul_sym x y z : ùîªR (fun ŒΩ => ŒΩ y * ŒΩ z = ŒΩ x).
  Proof. dio_sym (dio_rel_mul x y z). Defined.

  Fact dio_rel_fun x t : ùîªF t -> ùîªR (fun ŒΩ => ŒΩ x = t ŒΩ).
  Proof.
    apply dio_rel_ren
      with (œÅ := fun n =>
        match n with 
          | 0   => x 
          | S n => n 
        end).
  Defined.

  Fact dio_rel_fun_sym x t : ùîªF t -> ùîªR (fun ŒΩ => t ŒΩ = ŒΩ x).
  Proof. intros H; dio_sym (dio_rel_fun x H). Defined.

End utilities_for_better_efficiency.

Fact dio_fun_var x : ùîªF (fun ŒΩ => ŒΩ x).
Proof. apply dio_rel_eq. Defined.

Fact dio_fun_cst c : ùîªF (fun _ => c).
Proof. red; apply dio_rel_cst. Defined.

Fact dio_fun_add_im x y : ùîªF (fun ŒΩ => ŒΩ x + ŒΩ y).
Proof. apply dio_rel_add. Defined.

Fact dio_fun_mul_im x y : ùîªF (fun ŒΩ => ŒΩ x * ŒΩ y).
Proof. apply dio_rel_mul. Defined.

(** Automation *)

Create HintDb dio_rel_db.        (* For closure props ending with ùîªR _ *)
Create HintDb dio_fun_db.        (* For closure props ending with ùîªF _ *)
Create HintDb dio_rel_im_db.

Ltac dio_fun_auto := 
      apply dio_fun_var
   || apply dio_fun_cst
   || apply dio_fun_add_im
   || apply dio_fun_mul_im
   || auto 7 with dio_fun_db.   (* the depth of 7 is mostly enough *)

Hint Resolve dio_rel_eq 
             dio_rel_cst dio_rel_cst_sym
             dio_rel_add dio_rel_add_sym
             dio_rel_mul dio_rel_mul_sym : dio_rel_im_db.

Ltac dio_rel_immediate := auto with dio_rel_im_db.

Ltac dio_rel_eq := 
      apply dio_rel_fun         (* x = t *) 
   || apply dio_rel_fun_sym     (* t = x *)
   || apply dio_rel_fun_eq.     (* r = t *)

Ltac dio_rel_decompose :=
      apply dio_rel_exst
   || apply dio_rel_conj 
   || apply dio_rel_disj.

Ltac dio_rel_auto := 
   dio_rel_immediate;
   auto 7 with dio_rel_db dio_fun_db;
   (  (dio_rel_eq; dio_fun_auto) 
   || (dio_rel_decompose; dio_rel_auto)
   || idtac ).
   
(** this is the user level-tactic *)

Tactic Notation "dio" "auto" :=
  match goal with
    | |- ùîªR _ => dio_rel_auto
    | |- ùîªF _ => dio_fun_auto
  end.

Fact dio_fun_equiv r s : (forall ŒΩ, s ŒΩ = r ŒΩ) -> ùîªF r -> ùîªF s.
Proof.
  intros H1; apply dio_rel_equiv.
  abstract (intro; rewrite H1; tauto).
Defined.

(** Two syntactic sugar tactics *)

Tactic Notation "by" "dio" "equiv" uconstr(f) :=
  match goal with 
    | |- ùîªR _ => apply dio_rel_equiv with (R := f); [ | dio_rel_auto ]
    | |- ùîªF _ => apply dio_fun_equiv with (r := f); [ | dio_fun_auto ]
  end.

Tactic Notation "dio" "by" "lemma" uconstr(f) :=
  intros; apply dio_rel_equiv with (1 := f); dio auto. 

Fact dio_fun_ren t f : ùîªF t -> ùîªF (fun ŒΩ => t (fun n => ŒΩ (f n))).
Proof. apply dio_rel_ren with (œÅ := der_lift f). Defined.

Hint Resolve dio_fun_var dio_fun_cst dio_fun_ren : dio_fun_db.

(** We do not want dio_rel_[add,mul] is the dio_rel_db because
    they would superseed dio_fun_[plus,mult] *)

Fact dio_fun_plus r t : ùîªF r -> ùîªF t -> ùîªF (fun ŒΩ => r ŒΩ + t ŒΩ).
Proof.
  intros H1 H2; red.
  by dio equiv (fun ŒΩ => exists b c, ŒΩ 0 = b + c /\ b = r ŒΩ‚≠≥ /\ c = t ŒΩ‚≠≥).
  abstract (intros v; split;
    [ exists (r v‚≠≥), (t v‚≠≥); auto
    | intros (? & ? & -> & -> & ->); auto ]).
Defined.

Fact dio_fun_mult r t : ùîªF r -> ùîªF t -> ùîªF (fun ŒΩ => r ŒΩ * t ŒΩ).
Proof.
  intros H1 H2; red.
  by dio equiv (fun ŒΩ => exists b c, ŒΩ 0 = b * c /\ b = r ŒΩ‚≠≥ /\ c = t ŒΩ‚≠≥).
  abstract (intros v; split;
   [ exists (r v‚≠≥), (t v‚≠≥); auto
   | intros (? & ? & -> & -> & ->); auto ]).
Defined.

Hint Resolve dio_fun_plus dio_fun_mult : dio_fun_db.

Local Fact example_1 : ùîªR (fun ŒΩ => ŒΩ 0 = ŒΩ 0).
Proof. dio auto. Defined.

Check example_1.
Eval compute in (proj1_sig example_1).

(** Now you can start witnessing the magic of 
    Diophantine shapes recognition *)

Section True_False.

  (* We use these here to avoid a less efficient encoding of True/False *)

  Hint Resolve dio_rel_cst dio_rel_add : dio_rel_db.

  Fact dio_rel_True : ùîªR (fun _ => True).
  Proof.
    by dio equiv (fun _ => exists x, x = 0).
    abstract (split; try tauto; exists 0; auto).
  Defined.

  Fact dio_rel_False : ùîªR (fun _ => False).
  Proof.
    by dio equiv (fun _ => exists x, x = 1 /\ x = 0).
    abstract (split; try tauto; intros (? & ? & ?); omega).
  Defined.

End True_False.

Hint Resolve dio_rel_True dio_rel_False : dio_rel_db.

Fact dio_rel_le_im x y : ùîªR (fun ŒΩ => ŒΩ x <= ŒΩ y).
Proof.
  by dio equiv (fun ŒΩ => exists a, ŒΩ y = a + ŒΩ x).
  abstract (intros v; split;
    [ intros H; exists (v y - v x); omega
    | intros (? & ->); omega ]).
Defined.

Fact dio_rel_lt_im x y : ùîªR (fun ŒΩ => ŒΩ x < ŒΩ y).
Proof.
  by dio equiv (fun ŒΩ => exists a b c, ŒΩ y = c + ŒΩ x /\ b = 1 /\ c = a + b).
  abstract (intros v; split;
    [ intros H; exists (v y - v x -1), 1, (v y - v x); omega
    | intros (? & ? & ? & -> & -> & ->); omega ]).
Defined.

Fact dio_rel_div_im x y : ùîªR (fun ŒΩ => divides (ŒΩ x) (ŒΩ y)).
Proof.
  by dio equiv (fun ŒΩ => exists a, ŒΩ y = a * ŒΩ x).
  unfold divides; tauto.
Defined.

Hint Resolve dio_rel_le_im dio_rel_lt_im  dio_rel_div_im : dio_rel_im_db.

Fact dio_rel_le r t : ùîªF r -> ùîªF t -> ùîªR (fun ŒΩ => r ŒΩ <= t ŒΩ).
Proof.
  intros H1 H2.
  by dio equiv (fun ŒΩ => exists a b, a <= b /\ a = r ŒΩ /\ b = t ŒΩ).
  abstract (intros v; split;
    [ exists (r v), (t v)
    | intros (? & ? & ? & -> & ->) ]; auto).
Defined.

Fact dio_rel_lt r t : ùîªF r -> ùîªF t -> ùîªR (fun ŒΩ => r ŒΩ < t ŒΩ).
Proof.
  intros H1 H2.
  by dio equiv (fun ŒΩ => exists a b, a < b /\ a = r ŒΩ /\ b = t ŒΩ).
  abstract (intros v; split;
    [ exists (r v), (t v)
    | intros (? & ? & ? & -> & ->) ]; auto).
Defined.

Hint Resolve dio_rel_lt dio_rel_le : dio_rel_db.

Fact dio_rel_neq r t : ùîªF r -> ùîªF t -> ùîªR (fun ŒΩ => r ŒΩ <> t ŒΩ).
Proof.
  intros H1 H2.
  by dio equiv (fun v => exists a b, (a < b \/ b < a) /\ a = r v /\ b = t v).
  abstract (intros v; split;
    [ exists (r v), (t v)
    | intros (? & ? & ?) ]; omega).
Defined.

Fact dio_rel_div r t : ùîªF r -> ùîªF t -> ùîªR (fun ŒΩ => divides (r ŒΩ) (t ŒΩ)).
Proof.
  intros H1 H2.
  by dio equiv (fun v => exists a b, divides a b /\ a = r v /\ b = t v).
  abstract (intros v; split;
    [ exists (r v), (t v)
    | intros (? & ? & ? & -> & ->) ]; auto).
Defined.

Hint Resolve dio_rel_neq dio_rel_div : dio_rel_db.

(** These example are better now *)

Local Fact example_le : ùîªR (fun ŒΩ => ŒΩ 0 <= ŒΩ 1).
Proof. dio auto. Defined.

Check example_le.
Eval compute in (proj1_sig example_le). 

Local Fact example_lt : ùîªR (fun ŒΩ => ŒΩ 0 < ŒΩ 1).
Proof. dio auto. Defined.

Check example_lt.
Eval compute in (proj1_sig example_lt). 

Local Fact example_div : ùîªR (fun ŒΩ => divides (ŒΩ 0) (ŒΩ 1)).
Proof. dio auto. Defined.

Check example_div.
Eval compute in (proj1_sig example_div). 

Section dio_fun_rem.

  (** The remainder function is Diophantine *)

  Let rem_equiv p x r : r = rem x p <-> (p = 0 /\ x = r)
                                      \/ (p <> 0 /\ r < p /\ exists n, x = n*p + r).
  Proof.
    split.
    + intro; subst.
      destruct (eq_nat_dec p 0) as [ Hp | Hp ].
      * left; split; auto; subst; rewrite rem_0; auto.
      * right; split; auto; split.
        - apply div_rem_spec2; auto.
        - exists (div x p);apply div_rem_spec1.
    + intros [ (H1 & H2) | (H1 & H2 & n & H3) ].
      * subst; rewrite rem_0; auto.
      * symmetry; apply rem_prop with n; auto.
  Qed.
 
  Fact dio_fun_rem p x : ùîªF p -> ùîªF x -> ùîªF (fun ŒΩ => rem (x ŒΩ) (p ŒΩ)).
  Proof.
    dio by lemma (fun v => rem_equiv (p v‚≠≥) (x v‚≠≥) (v 0)).
  Defined.

End dio_fun_rem.
  
Hint Resolve dio_fun_rem : dio_fun_db.

Section dio_rel_ndivides.

  (** The way it is done in the FSCD paper *)

  Let ndivides_eq x y : ~ (divides x y) <-> x = 0 /\ y <> 0 \/ exists a b, y = a*x+b /\ 0 < b < x.
  Proof.
    split.
    + intros H.
      destruct x as [ | x ].
      * left; split; auto; contradict H; subst; apply divides_0.
      * right; exists (div y (S x)), (rem y (S x)); split.
        - apply div_rem_spec1.
        - rewrite divides_rem_eq in H.
          generalize (@div_rem_spec2 y (S x)); intros; omega.
    + intros [ (H1 & H2) | (a & b & H1 & H2) ].
      * subst; contradict H2; revert H2; apply divides_0_inv.
      * rewrite divides_rem_eq.
        rewrite (div_rem_spec1 y x) in H1.
        apply div_rem_uniq in H1; try omega.
        apply div_rem_spec2; omega.
  Qed.
  
  Fact dio_rel_ndivides x y : ùîªF x -> ùîªF y -> ùîªR (fun ŒΩ => ~ divides (x ŒΩ) (y ŒΩ)).
  Proof.
    dio by lemma (fun v => ndivides_eq (x v) (y v)).
  Defined.

End dio_rel_ndivides.

Local Fact dio_rel_ndiv_example : ùîªR (fun ŒΩ => ~ divides (ŒΩ 0) (ŒΩ 1)).
Proof. apply dio_rel_ndivides; dio auto. Defined.

Eval compute in proj1_sig dio_rel_ndiv_example.

Section dio_rel_not_divides.

  (** A shorter way using already established rem *)

  Let not_divides_eq p x : ~ divides p x <-> rem x p <> 0.
  Proof. rewrite divides_rem_eq; tauto. Qed.

  Lemma dio_rel_not_divides x p : ùîªF x -> ùîªF p -> ùîªR (fun ŒΩ => ~ divides (x ŒΩ) (p ŒΩ)).
  Proof.
    dio by lemma (fun v => not_divides_eq (x v) (p v)).
  Defined.

End dio_rel_not_divides.

Hint Resolve dio_rel_not_divides : dio_rel_db.

Local Fact example_rem : ùîªR (fun ŒΩ => rem (ŒΩ 0) (ŒΩ 1) = ŒΩ 2 * ŒΩ 3).
Proof. dio auto. Defined.

Check example_rem.
Eval compute in (proj1_sig example_rem).

(** We do not automate the remaining closure props here because
    they are used only once or twice elsewhere *) 

(** Closure under composition is trivial with exists *)

Section dio_rel_compose.

  Variable (f : (nat -> nat) -> nat) (R : nat -> (nat -> nat) -> Prop).
  Hypothesis (Hf : ùîªF f) 
             (HR : ùîªR (fun ŒΩ => R (ŒΩ 0) ŒΩ‚≠≥)).

  Lemma dio_rel_compose : ùîªR (fun ŒΩ => R (f ŒΩ) ŒΩ).
  Proof.
    by dio equiv (fun v => exists y, y = f v /\ R y v).
    abstract(intros v; split;
     [ exists (f v); auto
     | intros (? & -> & ?); auto ]).
  Defined.

End dio_rel_compose.

Section dio_fun_compose.

  Variable (f : (nat -> nat) -> nat) (Hf : ùîªF f)
           (g : nat -> nat)          (Hg : ùîªF (fun ŒΩ => g (ŒΩ 0))).

  Lemma dio_fun_compose : ùîªF (fun ŒΩ => g (f ŒΩ)).
  Proof.
    red; by dio equiv (fun v => exists y, y = f v‚≠≥ /\ v 0 = g y).
    abstract(intros; split;
     [ exists (f ŒΩ‚≠≥); auto
     | intros (? & -> & ?); auto ]).
  Defined.

End dio_fun_compose.

Section multiple_exists.

  (** We do the construction by hand here to obtain a easy 
      computation of the size 

      So here is a (counter-)example where we do actually
      manipulate De Bruijn syntax directly
    *)

  Fixpoint df_mexists n f :=
    match n with 
      | 0   => f
      | S n => df_mexists n (‚àÉf)
    end.

  Fact df_mexists_size n f : df_size (df_mexists n f) = n + df_size f.
  Proof. 
    revert f; induction n as [ | n IHn ]; intros f; auto; simpl df_mexists.
    rewrite IHn; simpl; omega. 
  Qed.

  Fact df_mexists_size_Z n f : df_size_Z (df_mexists n f) = (Z.of_nat n + df_size_Z f)%Z.
  Proof.
    rewrite df_size_Z_spec, df_mexists_size, Nat2Z.inj_add, df_size_Z_spec; omega.
  Qed.

  (* We only use it once so there is no need to automatize it *)

  Lemma df_mexists_spec n f ŒΩ :
           ‚ü¶df_mexists n f‚üß ŒΩ
       <-> exists œÄ, ‚ü¶f‚üß (fun i => if le_lt_dec n i then ŒΩ (i-n) else œÄ i).
  Proof.
    revert f ŒΩ; induction n as [ | n IHn ]; intros f v.
    + simpl; split; [ intros H; exists (fun _ => 0) | intros (? & H) ]; revert H; 
        apply df_pred_ext; intros; f_equal; omega.
    + simpl df_mexists; rewrite IHn; split; intros (pi & Hpi).
      * revert Hpi; rewrite df_pred_exst.
        intros (u & Hu).
        exists (fun i => match i with 0 => u | S i => pi i end).
        revert Hu; apply df_pred_ext.
        Opaque le_lt_dec.
        simpl; intros [ | i ].
        - replace (0-S n) with 0 by omega; auto.
        - replace (S i - S n) with (i-n) by omega. 
          simpl; destruct (le_lt_dec (S n) (S i)); 
            destruct (le_lt_dec n i); auto; omega.
      * exists (fun i => pi (S i)).
        rewrite df_pred_exst; exists (pi 0).
        revert Hpi; apply df_pred_ext.
        intros [ | i ].
        - replace (0-S n) with 0 by omega; simpl; auto.
        - replace (S i - S n) with (i-n) by omega.
          Opaque le_lt_dec.
          simpl; destruct (le_lt_dec (S n) (S i)); 
            destruct (le_lt_dec n i); auto; omega.
  Qed.

End multiple_exists.



